plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.kotlin.plugin.serialization")
}

android {
    namespace = "com.ridesandshares.passenger"
    compileSdk = 35
    buildToolsVersion = "35.0.0"

    defaultConfig {
        applicationId = "com.ridesandshares.passenger"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
        }
    }

    buildFeatures {
        compose = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    testOptions {
        unitTests.isIncludeAndroidResources = true
        unitTests.isReturnDefaultValues = true
    }
}

tasks.withType(org.gradle.api.tasks.testing.Test::class.java).configureEach {
    val slideOut = project.findProperty("SLIDE_OUT")?.toString().orEmpty()
    val frameOut = project.findProperty("FRAME_OUT")?.toString().orEmpty()
    val tripOut = project.findProperty("TRIP_OUT")?.toString().orEmpty()
    val standaloneOut = project.findProperty("STANDALONE_OUT")?.toString().orEmpty()
    inputs.property("SLIDE_OUT", slideOut)
    inputs.property("FRAME_OUT", frameOut)
    inputs.property("TRIP_OUT", tripOut)
    inputs.property("STANDALONE_OUT", standaloneOut)
    maxHeapSize = "2g"
    systemProperty("robolectric.graphicsMode", "NATIVE")
    environment("SLIDE_OUT", slideOut)
    environment("FRAME_OUT", frameOut)
    environment("TRIP_OUT", tripOut)
    environment("STANDALONE_OUT", standaloneOut)
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.12.01")
    implementation(composeBom)
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.animation:animation")
    implementation("androidx.compose.foundation:foundation")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.activity:activity-compose:1.9.3")
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3")
    implementation("com.google.zxing:core:3.5.3")
    implementation(project(":trip"))

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.robolectric:robolectric:4.14.1")
    testImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
}
